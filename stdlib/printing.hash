//! This module contains printing utilities for the language.

math := import("math");

/// Simply print a character.
print_char := (c: char) -> () => {
    // @@FixMe: use `putwchar` or properly write a wide character to the 
    // system by encoding it as a utf-8 string...

    val := cast<char, u8>(c);
    libc::write(1, &raw val, 1);
}


/// Simple printing utility for integers. This function will allocate a small
/// buffer on the heap, write character by character into the buffer, use it for
/// printing, and then finally free the buffer.
///
/// This function is the equivalent of `printf("%d", value)` in C.
print_int := (value: i32) => {
    if value == 0 {
        print_char('0');
        return;
    }

    data := libc::malloc(12);
    buf: [u8] = Intrinsics::transmute(type SizedPointer, type [u8], SizedPointer(data, 12));
    len := i32_to_string(value, buf, 10);

    // Convert it back to a sized_ptr and trim the string
    new_buf := Intrinsics::transmute(type [u8], type SizedPointer, buf);
    new_buf.1 = len;

    message := Intrinsics::transmute(type SizedPointer, type str, new_buf);
    print(message);

    libc::free(data);
}

/// Simple printing utility for usize values. Similar to print_int but handles
/// the usize type which is commonly used for indices and memory sizes.
print_usize := (value: usize) => {
    if value == 0 {
        print_char('0');
        return;
    }

    // Allocate a buffer large enough to hold any usize value
    // For a 64-bit system, the maximum usize would need at most 20 characters 
    // plus a null terminator
    data := libc::malloc(24);
    buf: [u8] = Intrinsics::transmute(type SizedPointer, type [u8], SizedPointer(data, 24));
    len := usize_to_string(value, buf, 10);

    // Convert it back to a sized_ptr and trim the string
    new_buf := Intrinsics::transmute(type [u8], type SizedPointer, buf);
    new_buf.1 = len;

    message := Intrinsics::transmute(type SizedPointer, type str, new_buf);
    print(message);

    libc::free(data);
}

/// Helper function to convert a usize to a string
priv usize_to_string := (value: usize, buf: [u8], base: i32) -> usize => {
    mut i := 0usize;

    if value == 0 {
        buf[i] = 0u8;
        return i + 1usize;
    }

    // Note: No need to check for negative values since usize is unsigned
    
    mut val := value;
    // Keep dividing by the base until we get 0 and add the remainder.
    while val != 0 {
        rem := cast<usize, i32>(val % cast<i32, usize>(base));

        // Convert the remainder into a character.
        buf[i] = if rem > 9 {
            Intrinsics::cast(type i32, type u8, (rem - 10) + 65)
        } else {
            // We add `48` to denote the ASCII offset for numbers.
            Intrinsics::cast(type i32, type u8, rem + 48)
        };

        i += 1;
        val /= cast<i32, usize>(base);
    }

    reverse(buf, i);
    i
}

/// Print a f64 value.
///
/// @@Todo: make the `print_float` have a default argument of precision, rather
/// than having a separate function.
print_float := (value: f64) => {
    print_float_with_precision(value, 6)
}

priv print_float_with_precision := (value: f64, precision: i32) => {
    mut whole := Intrinsics::cast(type f64, type i32, value);
    temp := Intrinsics::cast(type i32, type f64, whole);

    prec := Intrinsics::cast(type i32, type f64, precision);
    mut decimal := math::abs(Intrinsics::cast(type f64, type i32, (value - temp) * (10. ^^ prec)));

    print_int(whole);
    print_char('.');
    print_int(decimal);
}


priv i32_to_string := (value: i32, buf: [u8], base: i32) -> usize => {
    mut i := 0usize;
    mut negative := false;

    if value == 0 {
        buf[i] = 0u8;
        return i + 1usize;
    }

    if (value < 0) && (base == 10) {
        negative = true;
        value = -value;
    }

    // Keep dividing by the base until we get 0 and add the remainder.
    while value != 0 {
        rem := value % base;

        // Convert the remainder into a character.
        buf[i] = if rem > 9 {
            Intrinsics::cast(type i32, type u8, (rem - 10) + 65)
        } else {
            // We add `60` to denote the ASCII offset for numbers.
            Intrinsics::cast(type i32, type u8, rem + 48)
        };

        i += 1;
        value /= base;
    }

    if negative {
        buf[i] = Intrinsics::cast(type char, type u8, '-');
        i += 1;
    }

    reverse(buf, i);
    i
}

priv reverse := (buf: [u8], len: usize) => {
    mut start:= 0usize;
    mut end := len - 1;

    while start < end {
        // Swap the characters.
        tmp := buf[start];
        buf[start] = buf[end];
        buf[end] = tmp;

        start += 1;
        end -= 1;
    }
}
